.. _extensions:

Extension modules
=================

.. _poisson_likelihood:

Poisson likelihood
^^^^^^^^^^^^^^^^^^

The following is an example of an custom extension module to evaluate a
simple liklihood function based on Poisson sampling distribution of count
data.

.. code-block:: cython

    #cython: cdivision=True
    #cython: boundscheck=False
    #cython: nonecheck=False
    #cython: wraparound=False

    from __future__ import division, print_function

    import numpy as np
    from cython.parallel cimport *
    from libc.math cimport pow, log, floor
    from libc.stdio cimport printf
    from libc.stdlib cimport calloc, malloc, free

    from GSL cimport (gsl_strerror,
                      gsl_spline,
                      gsl_spline_alloc, 
                      gsl_spline_init,
                      gsl_spline_free,
                      gsl_spline_eval,
                      gsl_interp_steffen,
                      gsl_interp_accel,
                      gsl_interp_accel_alloc,
                      gsl_interp_accel_free,
                      gsl_interp_accel_reset,
                      gsl_isnan,
                      gsl_isinf)

    ctypedef gsl_interp_accel accel

    def eval_loglike(size_t N_Ts,
                     double[::1] phase_edges,
                     double[:,::1] counts,
                     double[:,::1] source,
                     double[:,::1] background,
                     double[::1] source_phases,
                     double phase_shift):

        """ Evaluate the Poisson likelihood. """

        cdef:
            signed int ii
            size_t i, j, T
            double LOGLIKE

            gsl_spline **spline = <gsl_spline**> malloc(sizeof(gsl_spline*) * N_Ts)
            accel **acc = <accel**> malloc(N_Ts * sizeof(accel*))

            double[:,::1] STAR = np.zeros((source.shape[0], phase_edges.shape[0]),
                                          dtype = np.double)

        for T in range(N_Ts):
            acc[T] = gsl_interp_accel_alloc()
            spline[T] = gsl_spline_alloc(gsl_interp_steffen, source_phases.shape[0])

        cdef double PHASE, TOTAL_STAR = 0.0, SCALE_STAR, TOTAL_BG = 0.0, SCALE_BG

        for ii in prange(<signed int>source.shape[0],
                         nogil = True,
                         schedule = 'static',
                         num_threads = N_Ts,
                         chunksize = 1):
            i = <size_t> ii
            T = threadid()

            gsl_interp_accel_reset(acc[T])
            gsl_spline_init(spline[T], &(source_phases[0]), &(source[i,0]), source_phases.shape[0])

            for j in range(phase_edges.shape[0]):
                PHASE = phase_edges[j] + phase_shift

                if PHASE > 1.0:
                    PHASE = PHASE - 1.0
                elif PHASE < 0.0:
                    PHASE = 1.0 + PHASE

                STAR[i,j] = gsl_spline_eval(spline[T], PHASE, acc[T])

        for T in range(N_Ts):
            gsl_interp_accel_free(acc[T])
            gsl_spline_free(spline[T])

        free(spline)
        free(acc)

        for i in range(STAR.shape[0]):
            for j in range(STAR.shape[1]):
                TOTAL_STAR += STAR[i,j]
                TOTAL_BG += background[i,j]

        if TOTAL_STAR > 0.0:
            SCALE_STAR = 1.0e4 / TOTAL_STAR

        SCALE_BG = 1.0e4 / TOTAL_BG

        LOGLIKE = 0.0

        cdef double EXPEC = 0.0
        for i in range(STAR.shape[0]):
            for j in range(STAR.shape[1]):
                EXPEC = background[i,j] * SCALE_BG
                if TOTAL_STAR > 0.0:
                    EXPEC += STAR[i,j] * SCALE_STAR

                LOGLIKE -= EXPEC
                LOGLIKE += counts[i,j] * log(EXPEC)

        return LOGLIKE

.. _numerical_atmosphere:

A numerical atmosphere
^^^^^^^^^^^^^^^^^^^^^^

The following is a customised version of the template extension module
for surface radiation field evaluation.

.. code-block:: cython

    #cython: cdivision=True
    #cython: boundscheck=False
    #cython: nonecheck=False
    #cython: wraparound=False

    from libc.math cimport M_PI, sqrt, sin, cos, acos, log10, pow, exp, fabs
    from libc.stdlib cimport malloc, free
    from libc.stdio cimport printf, fopen, fclose, fread, FILE
    from GSL cimport gsl_isnan, gsl_isinf

    from xpsi.global_imports import _keV, _k_B

    cdef int SUCCESS = 0
    cdef int ERROR = 1

    cdef double erg = 1.0e-7
    cdef double k_B = _k_B
    cdef double keV = _keV
    cdef double k_B_over_keV = k_B / keV
    cdef int VERBOSE = 0

    ctypedef struct ACCELERATE:
        size_t **BN
        double **node_vals
        double **SPACE
        double **DIFF
        double **INTENSITY_CACHE
        double **VEC_CACHE

    # Modify this struct if useful for the user-defined source radiation field
    # Note that the members of DATA will be shared by all threads and are
    # statically allocated, whereas the members of ACCELERATE will point to
    # dynamically allocated memory, not shared by threads.

    ctypedef struct DATA:
        size_t ndims
        size_t N[4]
        size_t BLOCKS[3]
        const srcRadField_PRELOAD *p
        ACCELERATE acc

    #----------------------------------------------------------------------->>>
    # >>> User modifiable functions.
    # >>> Note that the user is entirely free to wrap thread-safe and
    # ... non-parallel external C routines from an external library.
    # >>> Thus the bodies of the following need not be written explicitly in
    # ... the Cython language.
    #----------------------------------------------------------------------->>>
    cdef void* init_srcRadField(size_t numThreads, const srcRadField_PRELOAD *const preload) nogil:
        # This function must match the free management routine free_srcRadField()
        # in terms of freeing dynamically allocated memory. This is entirely
        # the user's responsibility to manage.
        # Return NULL if dynamic memory is not required for the model

        cdef DATA *D = <DATA*> malloc(sizeof(DATA))
        D.p = preload

        D.ndims = 4

        D.N[0] = 35
        D.N[1] = 11
        D.N[2] = 67
        D.N[3] = 166

        D.BLOCKS[0] = 64
        D.BLOCKS[1] = 16
        D.BLOCKS[2] = 4

        cdef size_t T, i, j, k, l

        D.acc.BN = <size_t**> malloc(numThreads * sizeof(size_t*))
        D.acc.node_vals = <double**> malloc(numThreads * sizeof(double*))
        D.acc.SPACE = <double**> malloc(numThreads * sizeof(double*))
        D.acc.DIFF = <double**> malloc(numThreads * sizeof(double*))
        D.acc.INTENSITY_CACHE = <double**> malloc(numThreads * sizeof(double*))
        D.acc.VEC_CACHE = <double**> malloc(numThreads * sizeof(double*))

        for T in range(numThreads):
            D.acc.BN[T] = <size_t*> malloc(D.ndims * sizeof(size_t))
            D.acc.node_vals[T] = <double*> malloc(2 * D.ndims * sizeof(double))
            D.acc.SPACE[T] = <double*> malloc(4 * D.ndims * sizeof(double))
            D.acc.DIFF[T] = <double*> malloc(4 * D.ndims * sizeof(double))
            D.acc.INTENSITY_CACHE[T] = <double*> malloc(256 * sizeof(double))
            D.acc.VEC_CACHE[T] = <double*> malloc(D.ndims * sizeof(double))
            for i in range(D.ndims):
                D.acc.BN[T][i] = 0
                D.acc.VEC_CACHE[T][i] = D.p.params[i][1]
                D.acc.node_vals[T][2*i] = D.p.params[i][1]
                D.acc.node_vals[T][2*i + 1] = D.p.params[i][2]

                j = 4*i

                D.acc.SPACE[T][j] = 1.0 / (D.p.params[i][0] - D.p.params[i][1])
                D.acc.SPACE[T][j] /= D.p.params[i][0] - D.p.params[i][2]
                D.acc.SPACE[T][j] /= D.p.params[i][0] - D.p.params[i][3]

                D.acc.SPACE[T][j + 1] = 1.0 / (D.p.params[i][1] - D.p.params[i][0])
                D.acc.SPACE[T][j + 1] /= D.p.params[i][1] - D.p.params[i][2]
                D.acc.SPACE[T][j + 1] /= D.p.params[i][1] - D.p.params[i][3]

                D.acc.SPACE[T][j + 2] = 1.0 / (D.p.params[i][2] - D.p.params[i][0])
                D.acc.SPACE[T][j + 2] /= D.p.params[i][2] - D.p.params[i][1]
                D.acc.SPACE[T][j + 2] /= D.p.params[i][2] - D.p.params[i][3]

                D.acc.SPACE[T][j + 3] = 1.0 / (D.p.params[i][3] - D.p.params[i][0])
                D.acc.SPACE[T][j + 3] /= D.p.params[i][3] - D.p.params[i][1]
                D.acc.SPACE[T][j + 3] /= D.p.params[i][3] - D.p.params[i][2]

                D.acc.DIFF[T][j] = D.acc.VEC_CACHE[T][i] - D.p.params[i][1]
                D.acc.DIFF[T][j] *= D.acc.VEC_CACHE[T][i] - D.p.params[i][2]
                D.acc.DIFF[T][j] *= D.acc.VEC_CACHE[T][i] - D.p.params[i][3]

                D.acc.DIFF[T][j + 1] = D.acc.VEC_CACHE[T][i] - D.p.params[i][0]
                D.acc.DIFF[T][j + 1] *= D.acc.VEC_CACHE[T][i] - D.p.params[i][2]
                D.acc.DIFF[T][j + 1] *= D.acc.VEC_CACHE[T][i] - D.p.params[i][3]

                D.acc.DIFF[T][j + 2] = D.acc.VEC_CACHE[T][i] - D.p.params[i][0]
                D.acc.DIFF[T][j + 2] *= D.acc.VEC_CACHE[T][i] - D.p.params[i][1]
                D.acc.DIFF[T][j + 2] *= D.acc.VEC_CACHE[T][i] - D.p.params[i][3]

                D.acc.DIFF[T][j + 3] = D.acc.VEC_CACHE[T][i] - D.p.params[i][0]
                D.acc.DIFF[T][j + 3] *= D.acc.VEC_CACHE[T][i] - D.p.params[i][1]
                D.acc.DIFF[T][j + 3] *= D.acc.VEC_CACHE[T][i] - D.p.params[i][2]

        cdef double *address = NULL
        # Cache intensity
        for T in range(numThreads):
            for i in range(4):
                for j in range(4):
                    for k in range(4):
                        for l in range(4):
                            address = D.p.I + (D.acc.BN[T][0] + i) * D.p.S[0]
                            address += (D.acc.BN[T][1] + j) * D.p.S[1]
                            address += (D.acc.BN[T][2] + k) * D.p.S[2]
                            address += D.acc.BN[T][3] + l
                            D.acc.INTENSITY_CACHE[T][i * D.BLOCKS[0] + j * D.BLOCKS[1] + k * D.BLOCKS[2] + l] = address[0]

        # Cast for generalised usage in integration routines
        return <void*> D


    cdef int free_srcRadField(size_t numThreads, void *const data) nogil:
        # This function must match the initialisation routine init_srcRadField()
        # in terms of freeing dynamically allocated memory. This is entirely
        # the user's responsibility to manage.
        # The void pointer must be appropriately cast before memory is freed --
        # only the user can know this at compile time.
        # Just use free(<void*> data) iff no memory was dynamically
        # allocated in the function:
        #   init_srcRadField()
        # because data is expected to be NULL in this case

        cdef DATA *D = <DATA*> data

        cdef size_t T

        for T in range(numThreads):
            free(D.acc.BN[T])
            free(D.acc.node_vals[T])
            free(D.acc.SPACE[T])
            free(D.acc.DIFF[T])
            free(D.acc.INTENSITY_CACHE[T])
            free(D.acc.VEC_CACHE[T])

        free(D.acc.BN)
        free(D.acc.node_vals)
        free(D.acc.SPACE)
        free(D.acc.DIFF)
        free(D.acc.INTENSITY_CACHE)
        free(D.acc.VEC_CACHE)

        free(D)

        return SUCCESS

    #----------------------------------------------------------------------->>>
    # >>> Cubic polynomial interpolation.
    # >>> Improve acceleration properties... i.e. do not recompute numerical
    # ... weights or re-read intensities if not necessary.
    #----------------------------------------------------------------------->>>
    cdef double eval_srcRadField(size_t THREAD,
                                 double E,
                                 double mu,
                                 const double *const VEC,
                                 void *const data) nogil:
        # This function must cast the void pointer appropriately for use.
        cdef DATA *D = <DATA*> data

        cdef:
            size_t i = 0, ii
            double I = 0.0, temp
            double *node_vals = D.acc.node_vals[THREAD]
            size_t *BN = D.acc.BN[THREAD]
            double *SPACE = D.acc.SPACE[THREAD]
            double *DIFF = D.acc.DIFF[THREAD]
            double *I_CACHE = D.acc.INTENSITY_CACHE[THREAD]
            double *V_CACHE = D.acc.VEC_CACHE[THREAD]
            double vec[4]
            double E_eff = k_B_over_keV * pow(10.0, VEC[0])
            int update_baseNode[4]
            int CACHE = 0

        vec[0] = VEC[0]
        vec[1] = VEC[1]
        vec[2] = mu
        vec[3] = log10(E / E_eff)

        while i < D.ndims:
            # if parallel == 31:
            #     printf("\nDimension: %d", <int>i)
            update_baseNode[i] = 0
            if vec[i] < node_vals[2*i] and BN[i] != 0:
                # if parallel == 31:
                #     printf("\nExecute block 1: %d", <int>i)
                update_baseNode[i] = 1
                while vec[i] < D.p.params[i][BN[i] + 1]:
                    # if parallel == 31:
                    #     printf("\n!")
                    #     printf("\nvec i: %.8e", vec[i])
                    #     printf("\nBase node: %d", <int>BN[i])
                    if BN[i] > 0:
                        BN[i] -= 1
                    elif vec[i] <= D.p.params[i][0]:
                        vec[i] = D.p.params[i][0]
                        break
                    elif BN[i] == 0:
                        break

                node_vals[2*i] = D.p.params[i][BN[i] + 1]
                node_vals[2*i + 1] = D.p.params[i][BN[i] + 2]

                # if parallel == 31:
                #     printf("\nEnd Block 1: %d", <int>i)

            elif vec[i] > node_vals[2*i + 1] and BN[i] != D.N[i] - 4:
                # if parallel == 31:
                #     printf("\nExecute block 2: %d", <int>i)
                update_baseNode[i] = 1
                while vec[i] > D.p.params[i][BN[i] + 2]:
                    if BN[i] < D.N[i] - 4:
                        BN[i] += 1
                    elif vec[i] >= D.p.params[i][D.N[i] - 1]:
                        vec[i] = D.p.params[i][D.N[i] - 1]
                        break
                    elif BN[i] == D.N[i] - 4:
                        break

                node_vals[2*i] = D.p.params[i][BN[i] + 1]
                node_vals[2*i + 1] = D.p.params[i][BN[i] + 2]

                # if parallel == 31:
                #     printf("\nEnd Block 2: %d", <int>i)

            # if parallel == 31:
            #     printf("\nTry block 3: %d", <int>i)

            if V_CACHE[i] != vec[i] or update_baseNode[i] == 1:
                # if parallel == 31:
                #     printf("\nExecute block 3: %d", <int>i)
                ii = 4*i
                DIFF[ii] = vec[i] - D.p.params[i][BN[i] + 1]
                DIFF[ii] *= vec[i] - D.p.params[i][BN[i] + 2]
                DIFF[ii] *= vec[i] - D.p.params[i][BN[i] + 3]

                DIFF[ii + 1] = vec[i] - D.p.params[i][BN[i]]
                DIFF[ii + 1] *= vec[i] - D.p.params[i][BN[i] + 2]
                DIFF[ii + 1] *= vec[i] - D.p.params[i][BN[i] + 3]

                DIFF[ii + 2] = vec[i] - D.p.params[i][BN[i]]
                DIFF[ii + 2] *= vec[i] - D.p.params[i][BN[i] + 1]
                DIFF[ii + 2] *= vec[i] - D.p.params[i][BN[i] + 3]

                DIFF[ii + 3] = vec[i] - D.p.params[i][BN[i]]
                DIFF[ii + 3] *= vec[i] - D.p.params[i][BN[i] + 1]
                DIFF[ii + 3] *= vec[i] - D.p.params[i][BN[i] + 2]

                V_CACHE[i] = vec[i]

                # if parallel == 31:
                #     printf("\nEnd block 3: %d", <int>i)

            # if parallel == 31:
            #     printf("\nTry block 4: %d", <int>i)

            if update_baseNode[i] == 1:
                # if parallel == 31:
                #     printf("\nExecute block 4: %d", <int>i)
                CACHE = 1
                SPACE[ii] = 1.0 / (D.p.params[i][BN[i]] - D.p.params[i][BN[i] + 1])
                SPACE[ii] /= D.p.params[i][BN[i]] - D.p.params[i][BN[i] + 2]
                SPACE[ii] /= D.p.params[i][BN[i]] - D.p.params[i][BN[i] + 3]

                SPACE[ii + 1] = 1.0 / (D.p.params[i][BN[i] + 1] - D.p.params[i][BN[i]])
                SPACE[ii + 1] /= D.p.params[i][BN[i] + 1] - D.p.params[i][BN[i] + 2]
                SPACE[ii + 1] /= D.p.params[i][BN[i] + 1] - D.p.params[i][BN[i] + 3]

                SPACE[ii + 2] = 1.0 / (D.p.params[i][BN[i] + 2] - D.p.params[i][BN[i]])
                SPACE[ii + 2] /= D.p.params[i][BN[i] + 2] - D.p.params[i][BN[i] + 1]
                SPACE[ii + 2] /= D.p.params[i][BN[i] + 2] - D.p.params[i][BN[i] + 3]

                SPACE[ii + 3] = 1.0 / (D.p.params[i][BN[i] + 3] - D.p.params[i][BN[i]])
                SPACE[ii + 3] /= D.p.params[i][BN[i] + 3] - D.p.params[i][BN[i] + 1]
                SPACE[ii + 3] /= D.p.params[i][BN[i] + 3] - D.p.params[i][BN[i] + 2]

                # if parallel == 31:
                #     printf("\nEnd block 4: %d", <int>i)

            i += 1

        cdef size_t j, k, l, INDEX, II, JJ, KK
        cdef double *address = NULL
        # Combinatorics over nodes of hypercube; weight cgs intensities
        for i in range(4):
            II = i * D.BLOCKS[0]
            for j in range(4):
                JJ = j * D.BLOCKS[1]
                for k in range(4):
                    KK = k * D.BLOCKS[2]
                    for l in range(4):
                        address = D.p.I + (BN[0] + i) * D.p.S[0]
                        address += (BN[1] + j) * D.p.S[1]
                        address += (BN[2] + k) * D.p.S[2]
                        address += BN[3] + l

                        temp = DIFF[i] * DIFF[4 + j] * DIFF[8 + k] * DIFF[12 + l]
                        temp *= SPACE[i] * SPACE[4 + j] * SPACE[8 + k] * SPACE[12 + l]
                        INDEX = II + JJ + KK + l
                        if CACHE == 1:
                            I_CACHE[INDEX] = address[0]
                        I += temp * I_CACHE[INDEX]

        #if gsl_isnan(I) == 1:
            #printf("\nIntensity: NaN; Index [%d,%d,%d,%d] ",
                    #<int>BN[0], <int>BN[1], <int>BN[2], <int>BN[3])

        #printf("\nBase-nodes [%d,%d,%d,%d] ",
                    #<int>BN[0], <int>BN[1], <int>BN[2], <int>BN[3])

        if I < 0.0:
            return 0.0

        return I * pow(10.0, 3.0 * vec[0])


    cdef double eval_srcRadField_norm() nogil:
        # Source radiation field normalisation which is independent of the
        # parameters of the parametrised model -- i.e. cell properties, energy,
        # and angle.
        # Writing the normalisation here reduces the number of operations required
        # during integration.

        return erg / 4.135667662e-18
